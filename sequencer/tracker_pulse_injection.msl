COMMENT "8 calibratioin runs with the external pulser"
comment "automated" 
ODBGET "/Mu2e/ActiveRunConfiguration/UseRunInfoDB" , use_rundb

RUNDESCRIPTION "pulse injection run"
PARAM runs, "aaaaa", 8

run_duration       = 10
first_channel_mask =  1

comment "set tracker command and the path to find parameters, do it once"

odbset "/Mu2e/Commands/Tracker/Name"         , "control_roc_pulser_on"
odbset "/Mu2e/Commands/Tracker/ParameterPath", "/Mu2e/Commands/Tracker/DTC"

LOOP i, $runs
   
   msg "configure step $i, first_chanenl_mask=$first_channel_mask"
   
   comment "define command name and its parameters"

   ODBSET "/Mu2e/Commands/Tracker/DTC/control_roc_pulser_on/first_channel_mask",  $first_channel_mask
   ODBSET "/Mu2e/Commands/Tracker/DTC/control_roc_pulser_on/pulser_delay", 1000
   ODBSET "/Mu2e/Commands/Tracker/DTC/control_roc_pulser_on/duty_cycle"  ,   10
   ODBSET "/Mu2e/Commands/Tracker/DTC/control_roc_pulser_on/print_level" ,    0
   
   comment "execute command"
   
   odbset            "/Mu2e/Commands/Tracker/Finished", 0 , 0
   odbset            "/Mu2e/Commands/Tracker/Run"     , 1 , 1
   wait   ODBValue , "/Mu2e/Commands/Tracker/Finished", > , 0
   odbset            "/Mu2e/Commands/Tracker/Run"     , 0 , 0

   comment "configuration finished, proceed with the data taking"
   
   IF ($use_rundb != 0)
      script get_next_run_number.py
   ENDIF

  msg "step $i: beginning of the start transition"
  TRANSITION START
  msg "step $i: end  of the start transition, take data for $run_duration sec"
  WAIT Seconds, $run_duration
  msg "step $i: beginning of the STOP transition"
  TRANSITION STOP
  msg "step $i: end of the stop transition"
  wait seconds, 5
  first_channel_mask = $first_channel_mask*2
ENDLOOP